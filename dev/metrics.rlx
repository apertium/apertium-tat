DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" "<:>";
SOFT-DELIMITERS = "<,>";

LIST Divisions = "өяз" "карья" "волос" "волость" "губерна" "вилаят" "авыл" "шәһәр";
LIST Professions = "мулла" "сәүдәгәр" "мещан" "дворянин" "имам" "купес" "дамелла" "хатыйп" "мөдәррис" "хаҗи" "потомственный почетный гражданин" "личный почетный гражданин" "почетный граджанин" "второй гильдий купес" "подданный" "потомственный дворянин" "народный учитель" "билетный солдат" "1 нче гильдии купец" "второй гильдии купец";
LIST Punct = "[" "]" ;

SECTION

#MAP (@paternal_grandfather_name) TARGET (np ant m) IF ((-1 ("ибн")) OR (-1 ("бин")));
#MAP (@father_name) TARGET (np ant m) IF ((1 ("ибн")) OR (-1 ("вәләд")) OR (-1 ("ата" n px3sp)));
#MAP (@father_name) TARGET (np ant m) IF (1 ("ул" n px3sp));
#MAP (@father_name) TARGET (np ant m) IF
#	((1 ("ибн")) OR (-1 ("вәләд")) OR (-1 ("ата" n px3sp)) OR (1 ("ул" n px3sp)));
MAP (@father_name) TARGET (np ant m) IF
	( (-1 ("вәләд")) OR (-1 ("ата" n px3sp)) );
MAP (@paternal_grandfather_name) TARGET (np ant m) IF
	( (-1 ("ибн")) OR (-1 ("бин")) OR (1 ("ул" n px3sp)) OR (-1 (@father_name)) );


MAP (@residence_uyezd) TARGET (np top nom) IF (1 ("өяз" n px3sp));
MAP (@residence_qarya) TARGET (np top nom) IF (1 ("карья" n px3sp));
MAP (@residence_volos) TARGET (np top nom) IF ((1 ("волос" n px3sp)) OR (1 ("волость" n px3sp)));
MAP (@residence_gubernya) TARGET (np top nom) IF (1 ("губерна" n px3sp));


MAP (@mother_name) TARGET (np ant f) IF ((-1 ("әнкә" n px3sp)) OR (-1 ("ана" n px3sp)));
MAP (@maternal_grandfather_lastname) TARGET (np cog m) IF (1 ("кыз" n px3sp));

MAP (@maternal_grandfather_name) TARGET (np ant m) IF ((-1 ("бинт" n px3sp)) OR (-1 (@mother_name)));
MAP (@mother_name) TARGET (np ant f) IF (1 (@maternal_grandfather_name));
MAP (@maternal_grandfather_name) TARGET (np ant m) IF ((1 ("кыз" n px3sp)) OR (1 (@maternal_grandfather_lastname)));


MAP (@paternal_grandfather_lastname) TARGET (np cog m) IF (-1 (@paternal_grandfather_name));
MAP (@paternal_grandfather_lastname) TARGET (np cog m) IF (-1 ("ул" n px3sp));

MAP (@paternal_grandfather_origin) TARGET (np top nom) IF ((-1 (@paternal_grandfather_name)) OR (-1 (@paternal_grandfather_lastname)));
MAP (@paternal_grandfather_profession) TARGET Professions IF
	((-1 (@paternal_grandfather_origin)) OR ((-1 (@paternal_grandfather_lastname))));
MAP (@paternal_grandfather_lastname) TARGET (np cog m) IF (-1 (@paternal_grandfather_profession));

MAP (@father_profession) TARGET Professions IF (-1 (np top));
MAP (@father_profession) TARGET Professions IF ((-1 (@family_name)) OR (-1 (@father_name)));

MAP (@father_origin) TARGET (np top nom) IF (1 (@father_profession)) ;
MAP (@father_name) TARGET (np ant m) IF (1 (@paternal_grandfather_name)) ;

MAP (@family_name) TARGET (np cog m) IF ((-1 (@father_name)) OR (-1 (@paternal_grandfather_name)));



REMOVE (top) IF (NOT 1 Professions) ;
REMOVE ("и" cop) ;
REMOVE (attr) ;


#### GUESSER FUNCTIONS ####

MAP (@guess_ANT-M) TARGET ("\\*.*"r) IF ((1 ("кыз" n px3sp)) OR (1 ("ул" n px3sp)));
MAP (@guess_ANT-M) TARGET ("\\*.*"r) IF ((-1 ("бинт" n px3sp)) OR (-1 ("ибн" n px3sp)) OR (-1 ("вәләд")) OR (-1 ("бин")));

#MAP (@guess_ANT-F) TARGET ("\\*.*"r) IF (((-1 ("әнкә" n px3sp)) OR (-1 ("ана" n px3sp))) (0 NOT Punct));
MAP (@guess_ANT-F) TARGET ("\\*.*"r) IF (-1 ("әнкә" n px3sp) OR ("ана" n px3sp)) (NOT 0 Punct);

MAP (@guess_COG-M) TARGET ("\\*[А-Я].*[ое]в"r);

MAP (@guess_TOP) TARGET ("\\*.*"r) IF (1 Divisions) (1 (n px3sp));


AFTER-SECTIONS

# this stuff removes readings that don't have @tags on words that otherwise got them
MAP (@X) TARGET (*);
REMOVE (@X);