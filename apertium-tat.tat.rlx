
# ============================================================================ #
#            A COMMENTARY ON FORMATTING OF MAJORITY OF RULES
#              (read: a wannabe FORMATTING GUIDELINES)
# ============================================================================ #

# Lines do not exceed 99 characters.
#
# Rule layout
# ===========
#
# There are two alternative layouts for writing a rule. An example for the
# first one (of course the body of the rule won't be commented out):
#
#        # INPUT: ^терістік/тер<v><tv><coop><ifi><p1><pl>/терістік<n><attr>/
#        #        терістік<n><nom>$ ^бетіндегі/бет<n><px3sp><loc><attr>/
#        #        бет<n><px3sp><loc><subst><nom>/$
#        # OUTPUT: [0]^терістік/терістік<n><attr>/терістік<n><nom>$
#                   ^бетіндегі/бет<n><px3sp><loc><attr>...$
#         REMOVE V IF
#                (0 PRE-N)
#                (1C N)
#        ;
#        # any additional comments here (if necessary)
#
# An abstract example of the second layout:
#
#        # An explanation of what the rule does (if it's not obvious from
#        # code!), a name or any other comment that clarifies its purpose
#         SELECT ABC IF
#                (0 DEF)
#                (1 GHI)
#        ;
#        ## A sentence with the [0]form the rule was written for.
#        ## (!) A corner-case-sentence: the rule mispredicted on it initially
#        ## but was modified accordingly (additional constraints etc.); or the
#        ## rule still mispredicts on it, but this kind of sentences aren't
#        ## that frequent. (Alternatively, place the corner-case sentence on
#        ## the same line as the additional constraint).
#
# The first layout is more appropriate when you come up with a rule inductively
# (i.e. you see a sentence badly translated because of errors in disambiguation
# and want to fix it), the second one when you arrived at it deductively. E.g:
#
#        # Participle appears immediately before a verb (verbal adverb doesn't)
#         REMOVE Participle IF
#                (NOT 1C V OR Vaux)
#        ;
#        ## Есік [0]ашып, ол үйге кірді.
#
# In context of apertium-tat, the difference between a participle and a verbal
# adverb is that participle immediately precedes a verb and verbal adverb
# doesn't. We know this even without looking at tests, but it is a good idea
# to back up the rule with a test case.
#
# Sometimes you can't fully disambiguate a form with only one rule. In that
# case, several rules dealing with the same thing can be grouped together:
#
#         # What the rule does?
#          SELECT ABC IF
#                 (0 DEF)
#                 (1 KLM)
#         ;
#          REMOVE XYZ IF
#                 (0 DEF)
#                 (1 NOP)
#                 (2 QRS)
#          ;
#          ## Example sentences for both of the rules.
#          ## Another example sentence.
#
# Where a rule is placed?
# =======================
#
#                 Disambiguates a particular wordform?
#                   /                         \
#                 YES                         NO
#                 /                             \
#     "Wordform/lemma specific rules"        Readings of
#     section at the end of the file        different POS?
#                                           /           \
#                                        YES            NO
#                                       /                 \
#                                "POS marking"             \
#                              at the beginning             \
#                                                   Corresponding section
#                                                   ("Verbs", "Adjectives" etc.)

# ============================ #
# END OF FORMATTING GUIDELINES #
# ============================ #

# ========== #
# Delimiters #
# ========== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;
SOFT-DELIMITERS = "<,>" ;

SUBREADINGS = LTR ; # Alternate, left-to-right (main reading on the left)

# ============= #
# Tags and sets #
# ============= #

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

# First-level/Parts-of-speech tags
# ================================

LIST A = adj ;
LIST Adv = adv ;
LIST Pron = prn ;
LIST N = n ;
LIST Prop = np ;
LIST V = v ;
LIST Vaux = vaux ;
LIST Cop = cop ;
LIST Det = det ;
LIST CC = cnjcoo ;
LIST CS = cnjsub ;
LIST Interj = ij ;
LIST Num = num ;
LIST Post = post ;
LIST Postadv = postadv ;
LIST Cm = cm ;
LIST Rquot = rquot ;

# POS sub-categories
# ==================

LIST Pers = pers ;
LIST Dem = dem;
LIST Interr = itg ;

# "Syntactic" tags
# ================

LIST Advl = advl ;
LIST Attr = attr ;
LIST Subst = subst ;

# Morphosyntactic properties   # if you find yourself embracing the same tag in
# ==========================   # brackets over and over again, you may want to
                               # add it here

LIST Sg = sg ;
LIST Pl = pl ;

LIST Nom = nom ;
LIST Gen = gen ;
LIST Dat = dat ;
LIST Acc = acc ;
LIST Abl = abl ;
LIST Loc = loc ;

LIST P1 = p1 ;
LIST P3 = p3 ;

LIST PersonalPossessives = px1sg px2sg px3sp px1pl px2pl ;

# Adjective sets
# ==============

LIST A/Advl = (adj advl) ;
LIST A/Subst = (adj subst nom) ;
SET A1 = A/Advl | A/Subst ;

# Verb sets
# =========

LIST FiniteVerb = pres past ifi  fut fut2 imp opt;
LIST Gerund = ger ger_ppot ger_past ger_perf ger_impf ger_abs ;
SET V-NotGerund = V - Gerund ;
LIST Pres = pres ;
LIST Participle = prc_perf prc_impf prc_cond prc_vol prc_plan ;
LIST VerbalAdverb = gna_perf gna_cond gna_until gna_after ;
LIST Prc-Impf = prc_impf ;
LIST Inf = inf ;
LIST Ger6 = ger6 ;
LIST TV = tv ;
LIST Opt = opt ;

# All possible word categories
# ============================

SET WORD = N | V | A | Post | Postadv | Pron | Det | Adv | CC | CS | Interj |
           Num | ("\?") ;

SET PRE-N =  A | Det | Postadv | Num | (n gen) | (prn gen) | CC ;

SET MARK =  Cm | ("\\") | ("\;") ;

SET WORDMARK = WORD | MARK ;

SET N-MOD = A | Det | Num | (n gen) | (prn gen) ;

SET ADJ-MOD = Postadv | Adv ;

SET NPMARK = N-MOD | ADJ-MOD ;

SET NOMINAL = N | Prop | Pron | Subst | Gerund ;

# Categories which cannot be part of a noun phrase
# ================================================

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;

# Barriers
# ========

SET S-BOUNDARY = CS | Interr | EOS;


# ======= #
  SECTION
# ======= #


#
 REMOVE Sub:1 Cop IF
        (NOT 1 EOS OR MARK)
;
##


#
 "<шул>" SELECT (mod_ass) IF
        (1 (sent))
;
## Кешеләр этләр белән сөйләшкәндә җавап алмыйлар шул.

# FIXME this rule mispredicts a lot (or, rather, block other rules selecting a non-copula reading)
 SELECT Sub:1 Cop IF
        (NOT -1 BOS)    # Single-word phrases like "Декабрь."
        (1 EOS OR MARK)
;
## TODO (!) 13 . [0]Жоқ, Айгүлдің күшігі жоқ, оның мысығы бар.
## TODO (!) Дұрыс, оның мысығы бар.


# INPUT:  ^Без/без<prn><pers><p1><pl><nom>/без<prn><pers><p1><pl><nom>+и<cop><p3><sg>$
#         ^бәхетлебез/бәхетле<adj>+и<cop><p1><pl>/бәхетле<adj><subst><nom>+и<cop><p1><pl>/
#         бәхетле<adj><subst><px1pl><nom>/бәхетле<adj><subst><px1pl><nom>+и<cop><p3><sg>$^./.<sent>$
# OUTPUT: ^Без/без<prn><pers><p1><pl><nom>/без<prn><pers><p1><pl><nom>+и<cop><p3><sg>$
#         [0]^бәхетлебез/бәхетле<adj>+и<cop><p1><pl>/бәхетле<adj><subst><nom>+и<cop><p1><pl>/
#         бәхетле<adj><subst><px1pl><nom>$^./.<sent>$
 REMOVE SUB:1 P3 IF
       (0/1 Cop)
       (0/1 P1) ;
##


# INPUT:  ^Без/без<prn><pers><p1><pl><nom>/без<prn><pers><p1><pl><nom>+и<cop><p3><sg>$
#         ^укытучылар/укытучы<n><nom>+и<cop><p3><pl>/укытучы<n><pl><nom>/
#         укыт<v><tv><gpr_pot><subst><pl><nom>/укытучы<n><pl><nom>+и<cop><p3><sg>$
# OUTPUT: ^Без/без<prn><pers><p1><pl><nom>/без<prn><pers><p1><pl><nom>+и<cop><p3><sg>$
#         [0]^укытучылар/укытучы<n><pl><nom>/укыт<v><tv><gpr_pot><subst><pl><nom>$
 REMOVE SUB:1 Pl IF
        (0/1 Cop + P3)
        (NOT -1* ("<Алар>"))
;


# ======= #
  SECTION
# ======= #

# INPUT:  ^Без/без<prn><pers><p1><pl><nom>$ 
#         ^укытучылар/укытучы<n><pl><nom>+и<cop><p3><sg>$^./.<sent>$
# OUTPUT: ^Без/без<prn><pers><p1><pl><nom>$ 
#         ^укытучылар/укытучы<n><pl><nom>+и<cop><p1><pl>$^./.<sent>$
 SUBSTITUTE SUB:1 P3 P1 TARGET Cop IF (@1 ("без"i) + Pers) ;
 SUBSTITUTE SUB:1 Sg Pl TARGET Cop IF (@1 ("без"i) + Pers) ;


# ======= #
  SECTION
# ======= #



# Part-of-speech marking
# ======================



#
 SELECT FiniteVerb IF
        (1 EOS OR ("ди"i))   # FIXME s/.*/SentenceBoundary/
        (NOT 0 ("бар"i))     # FIXME a better way?
        (NOT 0 ("түгел"i))
;
## (!) 40 . Мүмкін бұл Азамат [0]шығар?
##  (!) 13 . Жоқ, Айгүлдің күшігі жоқ, оның мысығы [0]бар.
## Мағынасы төңкеріліп [0]кетті деп ойламағаным үшін ғафу етіңіз.


#
 SELECT Interj IF
        (-1 BOS)
        (1 Cm)
;
## "Мысалы, ежелгі заманның өзінде Арал теңізі көп елдерге мәлім болған."


#
 REMOVE Post IF
        (NOT -1 NOMINAL)
        (NOT -1 ("белән"))
;
## песи белән [0]бергә

# Proper noun vs noun
 SELECT Prop IF
        (0 N)
        (0 Prop)
        (0 ("[:upper:]+[:lower:]*"r))
        (NOT -1 BOS)
;
## 108. Өйткені бүгінгі _Арал_ деген атау сол XVII ғасырдан бергі жерге берілген.
 SELECT Prop IF
        (0 N)
        (0 Prop)
        (0 ("[:upper:][:upper:]+"r))
;
## АЗАМАТ ҚАЙДА?
 SELECT Prop IF
        (0 Prop)
        (0 N)
        (1 ("белән") + Post)
        (2 Prop)
;
## Азамат белән Айгөл бакчада.


# INPUT:  ^Бу/Бу<det><dem>/Бу<prn><dem><nom>/Бу<v><tv><imp><p2><sg>/
#         Бу<prn><dem><nom>+и<cop><p3><sg>$ ^уен/уен<n><nom>/уен<n><attr>/
#         уй<n><px3sp><acc>/уен<n><nom>+и<cop><p3><sg>$^./.<sent>$
# OUTPUT: [0]^Бу/Бу<prn><dem><nom>$ ^уен/уен<n><nom>/уен<n><attr>/
#         уй<n><px3sp><acc>/уен<n><nom>+и<cop><p3><sg>$^./.<sent>$
 SELECT Pron IF
        (0 Det)
        (1  N)
        (2 EOS)
;


 SELECT A IF
        (0 N)
        (-1 Adv)
;
## Һава яхшы, бик [0]җылы.


 SELECT A IF
        (0 N)
        (-1 Cm)
        (-2 A)
;
## Һава яхшы, [0]җылы.



# Determiners
# ===========



REMOVE Det IF (NOT 1 NPMARK OR N OR Pron);
    #! Әхмәтнең кечкенә бер эте ^бар/бар<adj>/бар<adj><subst><nom>/бар<det><qnt>/бар<v><iv><imp><p2><sg>$ ,
    #! әлегә эт дә бакчада .
    ## FIXME:CHECK; 'Pron' was needed because 'нәрсә' is tagged as prn. Maybe we should consider it noun (as in Kazakh)'

SELECT Det IF (0C V OR Det OR A OR N) (1 NPMARK OR N) ;
    #! пример : Бар<det> җирдән карый, әмма Әхмәтне таба алмый.
    #! Акча бармы? Бар<adj>.
    #! Акча бармы? Бар<adj>, ләкин бик аз!

SELECT Det IF (-1C A) (1 N);
    #! Әхмәт тиз генә иске зур бер агачка йөгерә.
    #! Әхмәт акрын гына иске зур бер агачка йөгерә.


#
 SELECT Pron IF
        (0C Det OR Pron)
        (1 Adv)
;
## 44 . [0]Ол енді ол дыбысты анығырақ ести бастады.



# Nouns
# =====



#
 REMOVE Attr IF
       (0 N OR Prop)
       (NOT 0 Loc)
       (NOT 1 N OR Prop)
;
## 176. Орталықта Түркістанның төл тарихына арналған музейлік [0]экспозиция жасақталған.
## (!) 184 . Үшінші қабатта «Тәуелсіз [0]Қазақстандағы жаңа Түркістан» атты экспозициялар қойылған.


# Select attributive reading of the first noun of the II izafet construct
# (as Tatar grammars call it)
 SELECT Attr IF
        (0 N OR Prop)
        (1 N OR Prop OR Subst)
        (1 (px3sp))
;
## 111. Түркияның оңтүстік-шығысындағы [0]Газиантеп қаласында кеше болған жарылыстан қаза тапқандар саны 9-ға жетті.


# If a former gerund or verbal adjective was lexicalized as a noun, select noun
 SELECT N IF
        (0 N)
        (0 Gerund  OR (gpr_pot))
;
## professions, such as "оқушы"; -U gerunds , in theory other gerunds too



# Adjectives
# ==========



# select adverbial reading of adjectives if any verbal form except gerund follows
# FIXME CHECK it might be a gerund as well
 SELECT Advl IF
         (1C V-NotGerund)
;
##


# select pure adjective reading ("predicative" in this case) of an adjective if sentence ends
 REMOVE Advl IF
        (0 A1)
        (1 EOS)
;
 REMOVE Subst IF
        (0 A1)
        (1 EOS OR MARK)
;
##



# Postpositions
# =============



#
 "<таба>" SELECT Post IF
          (-1 Dat)
;
##


#
 "<бергә>" SELECT Post IF
        (-1 ("белән"))
;
##


#
 "<соң>" SELECT Post IF
                (-1 Abl)
;
## 23. Айгөл санап бетергәннән соң тирә-ягына карады.



# Verbs
# =====



REMOVE Participle IF
       (NOT 1C V OR Vaux)
;


# <prc_cond> vs. <gna_cond>
 REMOVE (prc_cond) IF
        (NOT 1 ("бул") + V)
;
## 34 . Ол Азаматтың қайда екенін [0]білсе де айтқысы келген жоқ.


#
 SELECT Participle IF
        (0C Participle OR VerbalAdverb)
        (1 Vaux)
;
## 16 . Азамат ескі үлкен бір ағашқа қарай қатты [?]жүгіріп бара жатыр, ол сол
## ағаштың артына Айгүлден [0]жасырынып жатыр.


#
 REMOVE Vaux IF
        (NOT -1 Participle)
;
## 187 . Біздің дәуірімізден 1 миллион жыл бұрын өмір сүрген "тік жүретін адамнан" [0]бастап, өз замандастарына дейінгі
## кезеңдерді қамтитын, ... орталықтың экспозициясы негізгі сегіз ірі бөлімнен тұрады.


SELECT Pres IF (0 Pres OR Prc-Impf) (-1C V);
    #! Эт бу ике бала белән уйнарга ^ярата/ярат<v><tv><prc_impf>/ярат<v><tv><pres><p3><sg>$.


#
 SELECT (gpr_past) IF
        (0C (ger_past) + Nom)
        (1 N)
;
##  172 . Сот арқылы немесе өзгеде осындай құзыреті бар мемлекеттік органдардың
## шешімі арқылы тәркіленген мұражайлық маңызы бар жинақтардың немесе [0]жекелеген заттардың есебінен;


#
 SELECT (prc_impf) IF
        (0 (pres p3))
        (1 Vaux)
;
## яза аласыз


# INPUT:  ^Мин/мин<prn><pers><p1><sg><nom>$ ^уйныйм/уйна<v><tv><opt><p1><sg>/уйна<v><tv><pres><p1><sg>$^./.<sent>$
# OUTPUT: ^Мин/мин<prn><pers><p1><sg><nom>$ ^уйныйм/уйна<v><tv><pres><p1><sg>$^./.<sent>$
 REMOVE Opt IF
        (NOT 1* ("!"))
;
#

###################################### TO BE SORTED ############################


SELECT A IF (0 ("бар")) (-1 PersonalPossessives );
    #! Әхмәтнең кечкенә бер эте ^бар/бар<adj>/бар<adj><subst><nom>/бар<det><qnt>/бар<v><iv><imp><p2><sg>$ .
    ## FIXME:CHECK


# RISKY
SELECT Det IF (0 ("берәр")) (1/-1 (px) OR Pron);
    #! Ул берәр хайванныкы да кошныкы да түгел.
    ## FIXME:CHECK:# 'Pron' was needed because 'нәрсә' is tagged as prn (e.g. "берәр нәрсә").


################################################################################



# Wordform/lemma-specific rules   # Adding new stuff here is discouraged.
# =============================   # Try to generalize, if possible, existing rules.


#
 "<үтә>" SELECT Adv IF
                (1 A)
;
## Бірақ кеше _өте_ суық еді!


# да<postadv/conj>
 SELECT SUB:1 Postadv IF
        (0 (gna_cond))
;
## 34 . Ол Азаматтың қайда екенін [0]білсе+[0/1_or_-1]де айтқысы келген жоқ.  # with SUBREADINGS = LTR ; option
## Ол Азаматтың қайла екенін [0]білсе+[0/1]де+[0/2_or_-1]мі?                  # with SUBREADINGS = LTR ; option


#
 SELECT Adv IF
        (0 ("бергә"))
        (NOT -1 ("белән"))
        (1 V)
;
## 6 . Азамат пен Айгүл ойнағанды жақсы көреді, олар әрдайым үлкен үйдің алдындағы бақшада [0]бірге ойнайды.
## (!) Ол Азаматпен бірге келді.


# INPUT:  ^Мин/Мин<prn><pers><p1><sg><nom>$
#         ^кичә/кич<v><tv><prc_impf>/кичә<adv>/кич<v><tv><pres><p3><sg>$
#         ^килдем/ки<v><tv><pass><ifi><p1><sg>/кил<vaux><ifi><p1><sg>/
#          кил<v><iv><ifi><p1><sg>$^./.<sent>$
# OUTPUT: ^Мин/Мин<prn><pers><p1><sg><nom>$
#         [0]^кичә/кичә<adv>$
#         ^килдем/ки<v><tv><pass><ifi><p1><sg>/кил<vaux><ifi><p1><sg>/
#          кил<v><iv><ifi><p1><sg>$^./.<sent>$
 REMOVE V + TV IF
        (0 ("кичә"i))
        (NOT -1* N + Acc)
;
#


# INPUT:  ^Ул/Ул<n><nom>/Ул<n><attr>/Ул<det><dem>/Ул<prn><dem><nom>/
#         Ул<prn><pers><p3><sg><nom>/Ул<n><nom>+и<cop><p3><sg>/
#         Ул<prn><dem><nom>+и<cop><p3><sg>$
#         ^көлә/көл<v><iv><prc_impf>/көл<v><iv><pres><p3><sg>$^./.<sent>$
# OUTPUT: [0]^Ул/Ул<det><dem>/Ул<prn><dem><nom>/Ул<prn><pers><p3><sg><nom>/
#         Ул<prn><dem><nom>+и<cop><p3><sg>$
#         ^көлә/көл<v><iv><prc_impf>/көл<v><iv><pres><p3><sg>$^./.<sent>$
 REMOVE N IF
        (0 ("ул"i))
        (NOT 0 PersonalPossessives)
;
#


# "ни" -- парный союз
"<ни>" REMOVE CC IF
        (NOT 1* ("ни"))
;
## Һәм Әхмәт агач артында ни эшли?


#
"<белән>" REMOVE CC IF
         (NOT 1 Nom)
;
## Куллары белән күзләрен каплый.


# INPUT:  ^килгәч/ки<v><tv><pass><gna_after>/кил<vaux><gna_after>/кил<v><iv><gna_after>$
# OUTPUT: ^килгәч/кил<vaux><gna_after>/кил<v><iv><gna_after>$
 REMOVE (pass) IF
        (0 ("ки"i))
;
# TODO: ^килгәч/ки<v><tv><pass><gna_after> is a transducer fail, fix there instead


#
"<яшь>" SELECT N IF
        (-1 Num)
        (1 EOS)
;        
## аңа биш яшь

#
 REMOVE V IF
        (0 ("бу"i))
        (NOT -1* Acc)
;
## ^Бу/бу<det><dem>/бу<prn><dem><nom>/бу<v><tv><imp><p2><sg>/бу<prn><dem><nom>+и<cop><p3><pl>/
## бу<prn><dem><nom>+и<cop><p3><sg>$

#
 "<ди>" SELECT Pres + P3 IF
        (1 EOS)
;
## ^ди/ди<v><tv><prc_impf>/ди<v><tv><imp><p2><sg>/ди<v><tv><pres><p3><sg>$

# 
 "<түгел>" SELECT Cop IF
        (1 EOS)
;
## ^түгел/түгел<cnjcoo>/и<cop><neg><p3><sg>/түк<v><tv><pass><imp><p2><sg>$
